//= Copyright 2009 (c) Centric Retail Solutions. All rights reserved ==========
// Packet : Flexpoint Development
// Customer: Castorama
// Unit   : FDetCAAuditCassiere.PAS : based on FDetGeneralCA (General
//                                      Detailform to print CAstorama rapports)
//------------------------------------------------------------------------------
// CVS   : $Header: /development/Castorama/Flexpoint/Src/Flexpoint201/Develop/FDetCAFluxFinanciers.pas,v 1.5 2010/09/23 07:18:45 BEL\DVanpouc Exp $
// History:
// - Started from Castorama - FlexPoint 2.0 - FDetCAAuditCassiere.PAS - CVS revision 1.9
//==============================================================================
// Version          ModifiedBy             Reason
// V 1.10           Teesta (TCS)           R2012.1 - CAFR - Nouveau_moyen_de_paiement

unit FDetCAFluxFinanciers;

//******************************************************************************

interface

uses
  Windows, Messages, SysUtils, Classes, Graphics, Controls, Forms, Dialogs,
  SfAutoRun, StdCtrls, CheckLst, Menus, ImgList, ActnList, ExtCtrls,
  ScAppRun, ScEHdler, Buttons, ComCtrls, ScDBUtil_BDE, OvcBase, OvcEF, OvcPB,
  OvcPF, ScUtils, SfVSPrinter7, SmVSPrinter7Lib_TLB, Db, DBTables,  RFpnTender,
  DFpnTenderCA, DFpnTenderGroup, DFpnTenderGroupCA, DFpnSafeTransaction,
  DFpnSafeTransactionCA, FDetGeneralCA;

//******************************************************************************
// Global definitions
//******************************************************************************

resourcestring     // of header
  CtTxtTitle       = 'List with the financial flow of the day';
  CtTxtOperateur   = 'Operator nr: %s';
  CtTxtCaisse      = 'Cash register report of: %s';
  CtTxtSafe        = 'Cash register type: %s';
  CtTxtPrintDate   = 'Printdate: %s at %s';
  CtTxtReportDate  = 'Reportdate: %s';

resourcestring     // of table header.
  CtTxtMouvement   = 'Mouvement type';
  CtTxtValide      = 'Validated by';
  CtTxtTime        = 'hh:mm:ss';
  CtTxtPayement    = 'Payment type';
  CtTxtQuantity    = 'Qty';
  CtTxtValue       = 'Value';
  CtTxtNoData      = 'No data for this daterange';
  CtTxtStartFuture = 'Startdate is in the future!';

resourcestring     // of table subtotals
  CtTxtTotal       = 'Total';

resourcestring     // of safes
  CtTxtGeneral     = 'General Safe';
  CtTxtMoney       = 'Money Safe';
  CtTxtExpense     = 'Expense Safe';

resourcestring     // of paymenttypes
  CtTxtTransfer    = 'Transfer';
  CtTxtPayIn       = 'Pay In';
  CtTxtPayOut      = 'Pay Out';
  CtTxtFinalCount  = 'Final Count';

const  // indicate that count and pay-in is generated by this application
  CtTxtCountCoffre = 'CodType: 131; IdtCheckout: 300';

//******************************************************************************
// Form-declaration.
//******************************************************************************

type
  // Create type for storing totals
  TRcdTotals =  class                 // Record to store tendergrouptotals
       IdtTenderGroup  : Integer;     // Identification of tendergroup
       TxtTenderGroup  : string;      // Name of tendergroup
       QtyValue        : Integer;     // Quantity for a tendergroup
       ValValue        : currency;    // Value for a tendergroup
  end;

  TFrmDetCAFluxFinanciers = class(TFrmDetGeneralCA)
    Panel3: TPanel;
    ChkLbxSafes: TCheckListBox;
    BtnSelectAllSafes: TBitBtn;
    BtnDeselectAllSafes: TBitBtn;
    procedure BtnPrintClick(Sender: TObject);
    procedure FormCreate(Sender: TObject);
    procedure BtnSelectAllSafesClick(Sender: TObject);
    procedure BtnDeselectAllSafesClick(Sender: TObject);
  protected
    LstTotals        : TList;               // List to store all totals records
    RcdTotals        : TRcdTotals;          // Record for handling listitems
    NumPagesPrinted  : Integer;             // number of pages on report
    FlgPrintHeader   : Boolean;              // Print pageheader
    // Formats of the rapport
    function GetFmtTableHeader : string; override;
    function GetFmtTableBody   : string; override;
    function GetTxtTableTitle  : string; override;
    function GetTxtTitRapport  : string; override;
    function GetTxtRefRapport  : string; override;
  public
    // Create safetransaction- en tendergrouplist
    procedure CreateLists; virtual;
    // Print a pageheader per operator
    procedure PrintReportHeader (NumOper     : Integer;
                                 FlgCheckout : Boolean); virtual;
    // Print a table per operator
    procedure PrintTable (NumOper      : Integer;
                          FlgCheckout  : Boolean); virtual;
    // Calculate the totals for an operator
    procedure CalculateValues(ObjTender : TObjTenderGroup;
                              qtyValue : Integer; valValue : Currency); virtual;
    // Print the totals for an operator
    procedure PrintTotals; virtual;
    // Reset lists
    procedure ResetValues; virtual;
    // Execute the report
    procedure Execute; override;
    // Determine the paymenttype of a saftransaction
    function GetPaymentType(CodType : Integer)                : string; virtual;
    //Determine the validator of a safetransaction
    function GetValidator(ObjSafeTrans : TObjSafeTransaction) : string; virtual;
  end;

var
  FrmDetCAFluxFinanciers: TFrmDetCAFluxFinanciers;

//******************************************************************************

implementation

uses
  StStrW,
  SfDialog,
  SmUtils,
  SmDBUtil_BDE,
  RFpnCom,

  DFpn,
  DFpnUtils,
  DFpnTradeMatrix;

{$R *.DFM}

//==============================================================================
// Source-identifiers
//==============================================================================

const  // Module-name
  CtTxtModuleName = 'FDetCAFluxFinanciers';

const  // PVCS-keywords
  CtTxtSrcName    = '$Source: E:/development/Castorama/Flexpoint/Src/Flexpoint201/Develop/FDetCAFluxFinanciers.pas,v $';
  CtTxtSrcVersion = '$Revision: 1.5 $';
  CtTxtSrcDate    = '$Date: 2010/09/23 07:18:45 $';

//******************************************************************************
// Implementation of TFrmDetCAFluxFinanciers
//******************************************************************************

function TFrmDetCAFluxFinanciers.GetFmtTableHeader: string;
var
  CntFmt           : integer;          // Loop for making the format.
begin  // of TFrmDetCAFluxFinanciers.GetFmtTableHeader
  Result     := '^+' + IntToStr (CalcWidthText (13, False));
  for CntFmt := 0 to 1 do
    Result := Result + TxtSep + '^+' + IntToStr (CalcWidthText (12, False));
  Result     := Result + TxtSep + '^+' + IntToStr (CalcWidthText (27, False));
  for CntFmt := 0 to 1 do
    Result := Result + TxtSep + '^+' + IntToStr (CalcWidthText (12, False));
end;   // of TFrmDetCAFluxFinanciers.GetFmtTableHeader

//==============================================================================

function TFrmDetCAFluxFinanciers.GetFmtTableBody: string;
var
  CntFmt           : integer;          // Loop for making the format.
begin  // of TFrmDetCAFluxFinanciers.GetFmtTableBody
  Result     := '<+' + IntToStr (CalcWidthText (13, False));
  for CntFmt := 0 to 1 do
    Result := Result + TxtSep + '<+' + IntToStr (CalcWidthText (12, False));
  Result     := Result + TxtSep + '<+' + IntToStr (CalcWidthText (27, False));
  for CntFmt := 0 to 1 do
    Result := Result + TxtSep + '>+' + IntToStr (CalcWidthText (12, False));
end;   // of TFrmDetCAFluxFinanciers.GetFmtTableBody

//==============================================================================

function TFrmDetCAFluxFinanciers.GetTxtTableTitle : string;
begin  // of TFrmDetCAFluxFinanciers.GetTxtTableTitle
  Result := CtTxtMouvement + TxtSep + CtTxtValide + TxtSep + CtTxtTime +
            TxtSep + CtTxtPayement + TxtSep + CtTxtQuantity  + TxtSep +
            CtTxtValue;
end;   // of TFrmDetCAFluxFinanciers.GetTxtTableTitle)

//==============================================================================

function TFrmDetCAFluxFinanciers.GetTxtTitRapport : string;
begin  // of TFrmDetCAFluxFinanciers.GetTxtTitRapport
  Result := CtTxtTitle;
end;   // of TFrmDetCAFluxFinanciers.GetTxtTitRapport

//==============================================================================

function TFrmDetCAFluxFinanciers.GetTxtRefRapport : string;
begin  // of TFrmDetCAFluxFinanciers.GetTxtRefRapport
  Result := inherited GetTxtRefRapport + '0012';         // FILL IN CORRECT REF.
end;   // of TFrmDetCAFluxFinanciers.GetTxtRefRapport

//==============================================================================
// TFrmDetCAFluxFinanciers.GetPaymentType : determines the paymenttype for a
// safetransaction.
//                                  -----
// INPUT   : CodType - the code for the paymenttype
//==============================================================================

function TFrmDetCAFluxFinanciers.GetPaymentType (CodType : Integer) : string;
begin  // of TFrmDetCAFluxFinanciers.GetPaymentType
  case CodType of
    CtCodSttPayInTransfer  : Result := CtTxtTransfer;
    CtCodSttPayOutTransfer : Result := CtTxtTransfer;
    CtCodSttPayInTender    : Result := CtTxtPayIn;
    CtCodSttPayOutTender   : Result := CtTxtPayOut;
    CtCodSttFinalCount     : Result := CtTxtFinalCount;
  else
    Result := ''
  end;
end;   // of TFrmDetCAFluxFinanciers.GetPaymentType

//==============================================================================
// TFrmDetCAFluxFinanciers.GetValidator : determines the validator for a
// safetransaction.
//                                  -----
// INPUT   : ObjSafetrans - the current safetransaction
//==============================================================================

function TFrmDetCAFluxFinanciers.GetValidator
                                 (ObjSafeTrans : TObjSafeTransaction) : string;
var
  StrLstValidation    : TStringList;          // Stringlist with validations
begin  // of TFrmDetCAFluxFinanciers.GetValidator
   StrLstValidation := LstSafeTransaction.GetExplanationForKeyword
                       (ObjSafeTrans, CtTxtFmtExplValidationCA);
   try
     if StrLstValidation.Count > 0 then begin
       Result := StrLstValidation.Strings [Pred (StrLstValidation.Count)];
       Result := Copy (Result, 1, AnsiPos (';', Result) - 1);
     end
     else
       Result := '';
   finally
      StrLstValidation.Free;
   end;
end;   // of TFrmDetCAFluxFinanciers.GetValidator

//==============================================================================
// TFrmDetCAFluxFinanciers.CreateLists : Create the safetransaction- and
// tendergrouplist
//==============================================================================

procedure TFrmDetCAFluxFinanciers.CreateLists;
begin  // of TFrmDetCAFluxFinanciers.CreateLists
  // Create instance of databasemodel SafetransactionCA
  if not Assigned (DMdFpnSafeTransactionCA) then
    DmdFpnSafeTransactionCA := TDmdFpnSafeTransactionCA.Create (Self);
  // Create instance of databasemodel TendergroupCD
  if not Assigned (DmdFpnTenderGroupCA) then
    DmdFpnTenderGroupCA := TDmdFpnTenderGroupCA.Create (Self);
  // Create instance of LstTendergroup
  if not Assigned (LstTenderGroup) then
    LstTenderGroup := TLstTenderGroup.Create;
  // Create instance of LstSafetransaction
  if not Assigned (LstSafeTransaction) then
     LstSafeTransaction := TLstSafeTransaction.Create;
  // Fill LstSafetransaction with data
  DmdFpnSafeTransactionCA.BuildListSafeTransAndDetailTotalFlux(
    LstSafeTransaction, DtmPckDayFrom.Date, DtmPckDayFrom.Date);
  DmdFpnUtils.CloseInfo;
  DmdFpnTenderGroupCA.BuildListTenderGroupActive (LstTenderGroup);              // Nouveau_moyen_de_paiement,R2012.1 - CAFR,Teesta
end;   // of TFrmDetCAFluxFinanciers.CreateLists

//==============================================================================
// TFrmDetCAFluxFinanciers.ResetValues : Clear or free all lists
//==============================================================================

procedure TFrmDetCAFluxFinanciers.ResetValues;
begin  // of TFrmDetCAFluxFinanciers.ResetValues
  LstTotals.Free;                           // Free LstTotals
  LstSafeTransaction.ClearSafeTransactions; // Clear data of LstSafetransaction
  LstTenderGroup.ClearTenderGroups;         // Clear data of LstTendergroups
end;   // of TFrmDetCAFluxFinanciers.ResetValues

//==============================================================================
// TFrmDetCAFluxFinanciers.CalculateValues : calculates the totals per
// tendergroup for an operator
//                                  -----
// INPUT   : ObjTender - the current tendergroup
//           qtyValue - variable to store the quantity
//           valValue - variable to store the value
//==============================================================================

procedure TFrmDetCAFluxFinanciers.CalculateValues(ObjTender : TObjTenderGroup;
                                       qtyValue : Integer; valValue : Currency);
var
  CntItem          : Integer;
  FldFound         : Boolean;
begin  // of TFrmDetCAFluxFinanciers.CalculateValues
  FldFound := False;
  // Loop the already found totals
  for CntItem := 0 to LstTotals.Count-1 do begin
    if LstTotals.Count <> 0 then begin
      RcdTotals := TRcdTotals(LstTotals.Items[CntItem]);
      // Update an existing tendergroup
      if RcdTotals.IdtTenderGroup = ObjTender.IdtTenderGroup then begin
        FldFound := True;
        RcdTotals.IdtTenderGroup := ObjTender.IdtTendergroup;
        RcdTotals.TxtTenderGroup := ObjTender.TxtPublDescr;
        RcdTotals.QtyValue := RcdTotals.QtyValue + qtyValue;
        RcdTotals.ValValue := RcdTotals.ValValue + valValue;
      end;
    end;
  end;
  // Insert a new tendergroup
  if not FldFound then begin
    RcdTotals := TRcdTotals.Create;
    RcdTotals.IdtTenderGroup := ObjTender.IdtTendergroup;
    RcdTotals.TxtTenderGroup := ObjTender.TxtPublDescr;
    RcdTotals.QtyValue    := qtyValue;
    RcdTotals.ValValue    := valValue;
    LstTotals.Add(RcdTotals);
  end;
end;    // of TFrmDetCAFluxFinanciers.CalculateValues

//==============================================================================
// TFrmDetCAFluxFinanciers.PrinTotals : print the totals per
// tendergroup for an operator
//==============================================================================

procedure TFrmDetCAFluxFinanciers.PrintTotals;
var
  TxtPrintline     : string;            // Line to print
  CntItem          : Integer;           // Counter
  TxtEspeces       : string;            // To compare tendergroup with
begin  // of TFrmDetCAFluxFinanciers.PrintTotals
  VspPreview.EndTable;
  VspPreview.StartTable;
  TxtEspeces := 'Espèces';
  // Loop all found totals
  for CntItem := 0 to LstTotals.Count-1 do begin
    RcdTotals := TRcdTotals(LstTotals.Items[CntItem]);
    // Print the first line with the 'Total' label in the first column
    if CntItem = 0 then begin
      // Print first line with 'Total'-label in first column
      if StrComp(PChar(RcdTotals.TxtTenderGroup), PChar(TxtEspeces)) = 0 then
        // Total of paymenttype 'Especes'
        TxtPrintLine := CtTxtTotal + TxtSep + '' + TxtSep + '' + TxtSep +
                        RcdTotals.TxtTenderGroup  + TxtSep +
                        '' + TxtSep + CurrToStr(RcdTotals.ValValue)
      else
        // Totals of all other paymenttypes
        TxtPrintLine := CtTxtTotal + TxtSep + '' + TxtSep + '' + TxtSep +
                        RcdTotals.TxtTenderGroup  + TxtSep +
                        IntToStr(RcdTotals.QtyValue) + TxtSep +
                        CurrToStr(RcdTotals.ValValue);
    end
    else begin
      // Print all other lines without 'Total'-label in first column
      if StrComp(PChar(RcdTotals.TxtTenderGroup), PChar(TxtEspeces)) = 0 then
        // Total of paymenttype 'Especes'
        TxtPrintLine := '' + TxtSep + '' + TxtSep + '' + TxtSep +
                        RcdTotals.TxtTenderGroup  + TxtSep +
                        '' + TxtSep + CurrToStr(RcdTotals.ValValue)
      else
        // Totals of all other paymenttypes
        TxtPrintLine := '' + TxtSep + '' + TxtSep + '' + TxtSep +
                        RcdTotals.TxtTenderGroup  + TxtSep +
                        IntToStr(RcdTotals.QtyValue) + TxtSep +
                        CurrToStr(RcdTotals.ValValue);
    end;
    VspPreview.AddTable (FmtTableBody, '', TxtPrintLine, clWhite,
                         clWhite, False);
    VspPreview.TableCell[tcRowBorderAbove, 1, 1, 1, 6] := 20;
    VspPreview.TableCell[tcFontBold, CntItem+1, 1, CntItem+1, 6] := True;
  end;
end;    // of TFrmDetCAFluxFinanciers.PrintTotals

//==============================================================================
// TFrmDetCAFluxFinanciers.PrinReportHeader : print a reportheader per operator
//==============================================================================

procedure TFrmDetCAFluxFinanciers.PrintReportHeader(NumOper     : Integer;
                                                    FlgCheckout : Boolean);
var
  TxtHdr           : string;           // Text stream for the header
  TxtOperatorNr    : string;           // Number of the operator
  TxtOperatorName  : string;           // Name of the operator
  TxtSafe          : string;           // Type of safe
begin  // of TFrmDetCAFluxFinanciers.PrintHeader
  // Print the title of the report
  TxtHdr := TxtTitRapport + CRLF + CRLF;
  // Print the operatornr and -name
  if NumOper = 9999 then
    TxtHdr := TxtHdr + CtTxtNoData + CRLF + CRLF
  else begin
    if flgCheckout then begin
      TxtSafe := ChkLbxSafes.Items.Strings[NumOper];
      TxtHdr  := TxtHdr + Format(CtTxtSafe, [TxtSafe]) + CRLF + CRLF;
    end
    else begin
      // Determine the operatornr of the checked chklbxoperator
      TxtOperatorNr   := Copy(ChkLbxOperator.Items.Strings[NumOper],1,
                         AnsiPos(':',ChkLbxOperator.Items.Strings[NumOper])-2);
      // Determine the operatorname of the checked chklbxoperator
      TxtOperatorName := Copy(ChkLbxOperator.Items.Strings[NumOper],
                         AnsiPos(':',ChkLbxOperator.Items.Strings[NumOper])+2,
                         Length(ChkLbxOperator.Items.Strings[NumOper]));
      TxtHdr := TxtHdr + Format(CtTxtOperateur, [TxtOperatorNr]) + CRLF;
      TxtHdr := TxtHdr + Format(CtTxtCaisse, [TxtOperatorName]) + CRLF + CRLF;
    end;
  end;
  // Print the report- en printdate
  TxtHdr := TxtHdr + Format (CtTxtReportDate,
            [FormatDateTime (CtTxtDatFormat, DtmPckDayFrom.Date)]) + CRLF;
  TxtHdr := TxtHdr + Format (CtTxtPrintDate,
            [FormatDateTime (CtTxtDatFormat, Now),
            FormatDateTime (CtTxtHouFormat, Now)]) + CRLF;
  VspPreview.Header := TxtHdr;
  FrmVSPreview.DrawLogo (Logo);
end;   // of TFrmDetCAFluxFinanciers.PrintHeader

//==============================================================================
// TFrmDetCAFluxFinanciers.PrinTable : print the detailtable per operator
//==============================================================================

procedure TFrmDetCAFluxFinanciers.PrintTable (NumOper      : Integer;
                                              FlgCheckout  : Boolean);
var
  TxtPrintLine         : string;               // String to print
  CntSafeTrans         : Integer;              // Counter for Safetransactions
  CntTender            : Integer;              // Counter for Tendergroup
  ObjSafeTransaction   : TObjSafeTransaction;  // object Safetransaction
  ObjTenderGroup       : TObjTenderGroup;      // object Tendergroup
  IdtOperatorNr        : string;               // Operator number
  TxtCurrentPayment    : string;               // Current paymenttype
  ValTotal             : Currency;             // Total value for a Safetrans.
  QtyTotal             : Integer;              // Total Quantity for a Safetrans.
  IdtCheckout          : Integer;              // Checkout number
  FlgFirstLineOfTrans  : Boolean;              // First line of transaction
  FlgNewPage           : Boolean;              // Add new page
begin  // of TFrmDetCAFluxFinanciers.PrintTable
  IdtCheckout     := 0;
  NumPagesPrinted := 0;              // Set pagecount to 0
  LstTotals       := TList.Create;   // Create list to store totals
  VspPreview.Orientation := orPortrait;
  VspPreview.StartDoc;
  LstTotals      := TList.Create;
  FlgPrintHeader := True;
  FlgNewPage     := True;
  CreateLists;
  VspPreview.TableBorder := tbBoxColumns;
  VspPreview.StartTable;
  if not FlgCheckout then
    IdtOperatorNr := Copy(ChkLbxOperator.Items.Strings[NumOper],1,
                     AnsiPos(':',ChkLbxOperator.Items.Strings[NumOper])-2)
  else
    IdtCheckout := Integer (ChkLbxSafes.Items.Objects[NumOper]);
  // Safetransaction loop
  for CntSafeTrans := 0 to pred(LstSafeTransaction.Count) do begin
    ObjSafeTransaction := LstSafeTransaction.SafeTransaction[CntSafeTrans];
    if (not FlgCheckout and (ObjSafeTransaction.IdtOperator = IdtOperatorNr))
    or (FlgCheckout and (ObjSafeTransaction.IdtCheckout = IdtCheckout))
    then begin
      if not(Copy(ObJSafeTransaction.StrLstExplanation.Text, 1, 30) =
             CtTxtCountCoffre) then begin
        if (NumPagesPrinted > 0) and FlgNewPage then begin
          VspPreview.NewPage;
          FlgNewPage      := False;
          NumPagesPrinted := NumPagesPrinted + 1;
        end;
        FlgFirstLineOfTrans := True;
        // Tendergroup loop
        for CntTender := 0 to pred(LstTenderGroup.Count) do begin
          ObjTenderGroup := LstTenderGroup.TenderGroup[CntTender];
          TxtCurrentPayMent := GetPaymentType(ObjSafeTransaction.CodType);
          if FlgCheckOut then begin
            TxtCurrentPayMent :=  CtTxtTransfer;
            if (ObjSafeTransaction.IdtCheckout = CtIdtSafe)
            and (Copy(ObJSafeTransaction.StrLstExplanation.Text, 1, 8) =
                 'Operator')  then
               TxtCurrentPayMent :=  CtTxtPayOut;
          end;
          if TxtCurrentPayMent <> '' then begin
            // Retrieve Totals
            LstSafeTransaction.LstTransDetail.TotalTransDetail(
                               ObjSafeTransaction.IdtSafeTransaction,
                               ObjSafeTransaction.NumSeq,
                               ObjTenderGroup.IdtTenderGroup,
                               QtyTotal, ValTotal);
            ValTotal := Round(ValTotal *100)/100;
            // Make value negative for pay-outs
            case ObjSafeTransaction.CodType of
              CtCodSttPayOutTender, CtCodSttFinalCount : ValTotal := -ValTotal
            else
              ValTotal := ValTotal;
            end;
            // Print line
            if QtyTotal > 0 then begin
              if FlgFirstLineOfTrans then begin
                FlgFirstLineOfTrans := False;
                VspPreview.EndTable;
                VspPreview.StartTable;
                // Print first line of safetransaction (with paymenttype)
                if ObjTenderGroup.IdtTenderGroup = 1 then begin
                  // Print line with paymenttype 'Especes' (without qty)
                  if ObjSafeTransaction.CodType = CtCodSttPayOutTransfer then
                    ValTotal := - ValTotal;
                    TxtPrintLine := TxtCurrentPayment + TxtSep +
                      GetValidator(ObjSafeTransaction) + TxtSep +
                      FormatDateTime (CtTxtHouFormat,
                      ObjSafeTransaction.DatReg) + TxtSep +
                      ObjTenderGroup.TxtPublDescr  + TxtSep +
                      '' + TxtSep + CurrToStr(ValTotal);
                end
                else
                  // Print lines of other paymenttypes (with qty)
                  TxtPrintLine := TxtCurrentPayment + TxtSep +
                    GetValidator(ObjSafeTransaction) + TxtSep +
                    FormatDateTime (CtTxtHouFormat, ObjSafeTransaction.DatReg) +
                    TxtSep + ObjTenderGroup.TxtPublDescr  + TxtSep +
                    IntToStr(QtyTotal) + TxtSep + CurrToStr(ValTotal);
              end
              else begin
                // Print other lines of safetrans. (without paymenttype)
                if ObjTenderGroup.IdtTenderGroup = 1 then
                  // Print line with paymenttype 'Especes' (without qty)
                  TxtPrintLine := '' + TxtSep +
                                  GetValidator(ObjSafeTransaction) + TxtSep +
                                  FormatDateTime (CtTxtHouFormat,
                                        ObjSafeTransaction.DatReg) + TxtSep +
                                  ObjTenderGroup.TxtPublDescr  + TxtSep +
                                  '' + TxtSep + CurrToStr(ValTotal)
                else
                  // Print lines of other paymenttypes (with qty)
                  TxtPrintLine := '' + TxtSep +
                            GetValidator(ObjSafeTransaction) + TxtSep +
                            FormatDateTime (CtTxtHouFormat,
                                         ObjSafeTransaction.DatReg) + TxtSep +
                            ObjTenderGroup.TxtPublDescr  + TxtSep +
                            IntToStr(QtyTotal) + TxtSep + CurrToStr(ValTotal)
              end;
              if FlgPrintHeader then
                PrintReportHeader(NumOper, FlgCheckOut);
              VspPreview.AddTable (FmtTableBody, '', TxtPrintLine, clWhite,
                                   clWhite, False);
              CalculateValues(ObjTenderGroup, QtyTotal, ValTotal);
              FlgPrintHeader := False;
            end;
          end;
        end;
      end;
    end;
  end;
  if not FlgPrintHeader then begin
    PrintTotals;
    NumPagesPrinted := NumPagesPrinted + 1;
  end;
  ResetValues;
  VspPreview.EndTable;
  if flgPrintHeader then
    PrintReportHeader (9999, True);
  VspPreview.EndDoc;
  PrintPageNumbers;
  PrintReferences;
  if FlgPreview then
    FrmVSPreview.ShowModal
  else
    VspPreview.PrintDoc (False, 1, VspPreview.PageCount);
end;   // of TFrmDetCAFluxFinanciers.PrintTableBody

//==============================================================================

procedure TFrmDetCAFluxFinanciers.Execute;
var
  CntItem          : Integer;   // variable for looping
begin  // of TFrmDetCAFluxFinanciers.Execute
  // Handle all checked operators
  for CntItem := 0 to Pred(ChkLbxOperator.Items.Count) do
    if ChkLbxOperator.Checked[CntItem] then
       PrintTable(CntItem, False);
  // Handle all checked checkout
  for CntItem := 0 to Pred(ChkLbxSafes.Items.Count) do
    if ChkLbxSafes.Checked[CntItem] then
       PrintTable(CntItem, True);
end;   // of TFrmDetCAFluxFinanciers.Execute

//==============================================================================

procedure TFrmDetCAFluxFinanciers.BtnPrintClick(Sender: TObject);
begin  // of TFrmDetCAFluxFinaciers.BtnPrintClick
  if DtmPckDayFrom.Date > Now then begin
    DtmPckDayFrom.Date := Now;
    MessageDlg (CtTxtStartFuture, mtWarning, [mbOK], 0);
  end
  else begin
    FlgPreview := (Sender = BtnPreview);
    Execute;
  end;
end;   // of TFrmDetCAFluxFinaciers.BtnPrintClick

//==============================================================================

procedure TFrmDetCAFluxFinanciers.FormCreate(Sender: TObject);
begin  // of TFrmDetCAFluxFinanciers.FormCreate
  inherited;
  ChkLbxSafes.Items.AddObject (CtTxtGeneral, TObject (CtIdtSafe));
  ChkLbxSafes.Items.AddObject (CtTxtMoney, TObject (CtIdtChangeSafe));
  ChkLbxSafes.Items.AddObject (CtTxtExpense, TObject (CtIdtExpenseSafe));
end;   // of TFrmDetCAFluxFinanciers.FormCreate

//==============================================================================

procedure TFrmDetCAFluxFinanciers.BtnSelectAllSafesClick(Sender: TObject);
var
  CntIx            : Integer;          // Looping all items in de listcheckbox
begin  // of TFrmDetGeneralCA.BtnSelectAllSafesClick
  inherited;
  for CntIx := 0 to Pred (ChkLbxSafes.Items.Count) do
    ChkLbxSafes.Checked [CntIx] := True;
end;   // of TFrmDetGeneralCA.BtnSelectAllSafesClick

//==============================================================================

procedure TFrmDetCAFluxFinanciers.BtnDeselectAllSafesClick(Sender: TObject);
var
  CntIx            : Integer;          // Looping all items in de listcheckbox
begin  // of TFrmDetGeneralCA.BtnDeselectAllSafesClick
  inherited;
  for CntIx := 0 to Pred (ChkLbxSafes.Items.Count) do
    ChkLbxSafes.Checked [CntIx] := False;
end;   // of TFrmDetGeneralCA.BtnDeselectAllSafesClick

//==============================================================================

initialization
  // Add module to list for version control
  AddPVCSSourceIdent (CtTxtModuleName, CtTxtSrcName, CtTxtSrcVersion,
                      CtTxtSrcDate);
end.   // of SfAutoRun
